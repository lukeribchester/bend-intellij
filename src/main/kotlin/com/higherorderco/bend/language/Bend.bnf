{
  parserClass="com.higherorderco.bend.language.parser.BendParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Bend"
  psiImplClassSuffix="Impl"
  psiPackage="com.higherorderco.bend.language.psi"
  psiImplPackage="com.higherorderco.bend.language.psi.impl"

  elementTypeHolderClass="com.higherorderco.bend.language.psi.BendTypes"
  elementTypeClass="com.higherorderco.bend.language.psi.BendElementType"
  tokenTypeClass="com.higherorderco.bend.language.psi.BendTokenType"

  tokens = [
    /* Whitespace */
    CRLF="regexp:\R"
    WHITE_SPACE="regexp:[ \n\t\f]"

    /* Comments */
    COMMENT="regexp:(#.*\n*)"

    /* Literals */
    CHAR="regexp:'(\\.|[^'])'"
    STRING="regexp:\"(\\.|[^\"])*\""
    DIGIT="regexp:[0-9]"
    NUMBER="regexp:([0-9]+|0x[0-9a-fA-F]+|0b[01]+)"

    /* Identifiers */
    NAME="regexp:[_\-./a-zA-Z0-9]*"
  ]
}

Book       ::= !<<eof>> (Data | Rule)*
Data       ::= "data" NAME "=" ( NAME | "(" NAME (NAME)* ")" )+
Rule       ::= ("(" NAME Pattern* ")" | NAME Pattern*) "=" Term
Pattern    ::= "(" NAME Pattern* ")" | NameEra | NUMBER | "(" Pattern ("," Pattern)+ ")"
Term       ::= NUMBER | NumOp | Tup | App | Group | Nat | Lam | UnscopedLam | Bend | Fold |
               Use | Dup | LetTup | Let | Bind | Match | Switch | Era | UnscopedVar | Var
Lam        ::= Tag? ("λ"|"@") NameEra Term
UnscopedLam::= Tag? ("λ"|"@") "$" NAME Term
NumOp      ::= "(" Operator Term Term ")"
Tup        ::= "(" Term ("," Term)+ ")"
App        ::= Tag? "(" Term (Term)+ ")"
Group      ::= "(" Term ")"
Use        ::= "use" NAME "=" Term ";"? Term
Let        ::= "let" NameEra "=" Term ";"? Term
Bind       ::= "do" NAME "{" Ask "}"
Ask        ::= "ask" Pattern "=" Term ";" Term | Term
LetTup     ::= "let" "(" NameEra ("," NameEra)+ ")" "=" Term ";"? Term
Dup        ::= "let" Tag? "{" NameEra (","? NameEra)+ "}" "=" Term ";"? Term
List       ::= "[" (Term ","?)* "]"

//String     ::= "\"" (escape sequence | [^"])* "\""
//Char       ::= "'" (escape sequence | [^']) "'"

Match      ::= "match" NAME ("=" Term)? ("with" Var (","? Var)*)? "{" MatchArm+ "}"
MatchArm   ::= "|"? Pattern ":" Term ";"?
Switch     ::= "switch" NAME ("=" Term)? ("with" Var (","? Var)*)? "{" SwitchArm+ "}"
SwitchArm  ::= "|"? (DIGIT+|"_") ":" Term ";"?
Var        ::= NAME
UnscopedVar::= "$" NAME
NameEra    ::= NAME | "*"
Era        ::= "*"
Tag        ::= "#" NAME

//Name       ::= [_\-./a-zA-Z0-9]+
//Number     ::= ([0-9]+ | "0x"[0-9a-fA-F]+ | "0b"[01]+)

Operator   ::= ( "+" | "-" | "*" | "/" | "%" | "==" | "!=" | "<<" | ">>" | "<=" | ">=" | "<" | ">" | "^" )
